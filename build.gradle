// (c) https://github.com/MontiCore/monticore

/* ============================================================ */
/* ========================= Plugins ========================== */
/* ============================================================ */

plugins {
    id 'java'
    id "monticore" version "6.3.0-SNAPSHOT"
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "de.set.ecj" version "1.4.1" // Eclipse compiler as it's much faster than javac
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

/* ============================================================ */
/* ========================= Project ========================== */
/* ============================================================ */

group = 'de.monticore'
version = '6.3.0-SNAPSHOT'
description = 'siunits'
sourceCompatibility = '1.8'

/* ============================================================ */
/* ========================= Versions ========================= */
/* ============================================================ */

def monticore_version = "6.3.0-SNAPSHOT"
def jscience_version = "4.3.1"
def junit_version = "4.12"
def commons_cli_version = "1.4"
def grammar_classifier = "grammars"
def grammarDir = "src/main/grammars"


/* ============================================================ */
/* ======================= Configuration ====================== */
/* ============================================================ */

// configure non-standard source sets
sourceSets {
    main.java.srcDirs += [ "$projectDir/target/generated-sources/monticore/sourcecode"]

    test {
        java.srcDir "$projectDir/src/test/java"
        resources.srcDir "$projectDir/src/test/resources"
    }
}
buildDir = file("$projectDir/target")

configurations {grammar}

test {
    useJUnit()
}

/* ============================================================ */
/* ======================= Dependencies ======================= */
/* ============================================================ */

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        credentials.username mavenUser
        credentials.password mavenPassword
      url repo
    }
}

dependencies {
    grammar "de.monticore:monticore-grammar:$monticore_version:$grammar_classifier"
    implementation "org.antlr:antlr4-runtime:4.7.1"
    implementation "de.monticore:monticore-generator:$monticore_version"
    implementation "de.monticore:monticore-runtime:$monticore_version"
    implementation "de.monticore:monticore-grammar:$monticore_version"
    implementation "org.assertj:assertj-core:3.7.0"
    implementation "commons-cli:commons-cli:$commons_cli_version"
	implementation "org.jscience:jscience:$jscience_version"
    testImplementation "junit:junit:$junit_version"
    compileOnly "de.monticore:monticore-grammar:$monticore_version"
}

/* ============================================================ */
/* ========================== Tasks =========================== */
/* ============================================================ */

task generateSIUnits(type: MCTask) {
    grammar = file "$projectDir/$grammarDir/de/monticore/SIUnits.mc4"
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    handcodedPath file("$projectDir/src/main/java")
    def uptoDate = incCheck("de/monticore/SIUnits.mc4")
    outputs.upToDateWhen { uptoDate }
}

compileJava.dependsOn(generateSIUnits)

// Fixes issue with java plugin in projects without resources
compileTestJava.doFirst { mkdir sourceSets.main.output.resourcesDir }

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.deprecation = false
	options.warnings = false
}

// build sources jar in addition
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

// build javadoc jar in addition
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

// generated java doc contains errors, disable for now
javadoc.failOnError(false)

// build grammar jar as well
task grammarJar(type: Jar) {
    from("$projectDir/src/main/grammars/") { include "**/*.mc4" }
    archiveClassifier = "grammars"
}

// all in one cli-jar
//shadowJar {
//    manifest {
//        attributes "Main-Class": "de.monticore.${archiveBaseName.get().capitalize()}CLI"
//    }
//    archiveFileName = "${archiveBaseName.get()}-cli.${archiveExtension.get()}"
//    minimize()
//}

//jar.dependsOn shadowJar

/* ============================================================ */
/* ======================= Publishing ========================= */
/* ============================================================ */

publishing {
    // configure what artifacts to publish
    publications {
        mavenJava(MavenPublication) {
            artifactId = "$project.name"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact grammarJar
        }
//        cli(MavenPublication) {
//            artifactId = "$project.name"
//            artifact shadowJar
//        }
    }
    repositories.maven {
        credentials.username mavenUser
        credentials.password mavenPassword
        def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
        def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
    }
}
